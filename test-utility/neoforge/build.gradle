import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id("com.kotori316.tu.common")
    id("com.kotori316.tu.platforms")
    id("net.neoforged.gradle.userdev") version("7.+")
    // id("org.parchmentmc.librarian.forgegradle") version("1.+")
}

String mod_id = "test_utility_neo"

println "Java: ${System.getProperty "java.version"}, JVM: ${System.getProperty "java.vm.version"} (${System.getProperty "java.vendor"}), Arch: ${System.getProperty "os.arch"}"
runs {
    /*client {
        workingDirectory.set(project.file("Minecraft"))
        systemProperties.put("forge.logging.markers", "REGISTRIES")
        systemProperties.put("mixin.env.remapRefMap", "true")
        systemProperties.put("mixin.env.refMapRemappingFile", "${projectDir}/build/createSrgToMcp/output.srg")
        systemProperties.put("mixin.debug.export", "true")
        systemProperties.put("forge.logging.console.level", "debug")
        programArguments.addAll('--username', "Kotori")

        modSources.add(project.sourceSets.main)
    }*/
}

subsystems {
    parchment {
        minecraftVersion = project.property("parchment_minecraft") as String
        mappingsVersion = project.property("parchment_mapping") as String
    }
}

repositories {
    mavenCentral()
    maven {
        name = "Azure-SLP"
        url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
        content {
            includeModule("org.typelevel", "cats-core_3")
            includeModule("org.typelevel", "cats-kernel_3")
            includeVersion("com.kotori316", "test_utility_dependency", "2.0-SNAPSHOT")
        }
    }
    maven {
        url = uri("https://prmaven.neoforged.net/NeoForge/pr794")
    }
}

def commonProject = project.findProject(":test-utility:common")
dependencies {
    implementation(group: "net.neoforged", name: "neoforge", version: project.property("neo_version").toString())

    if (commonProject != null) {
        compileOnly(commonProject)
    }
}

processResources {
    from (commonProject.sourceSets.main.resources)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named("compileJava", JavaCompile).configure {
    source(commonProject.sourceSets.main.allSource)
}

test {
    useJUnitPlatform()
}

def jarAttributeMap = [
        "Specification-Title"     : mod_id,
        "Specification-Vendor"    : "Kotori316",
        "Specification-Version"   : "1", // We are version 1 of ourselves
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : project.jar.archiveVersion,
        "Implementation-Vendor"   : "Kotori316",
        "Implementation-Timestamp": ZonedDateTime.now().format(DateTimeFormatter.ISO_INSTANT),
        "MixinConfigs"            : "${mod_id}.mixins.json",
        "Automatic-Module-Name"   : mod_id,
]

jar {
    manifest {
        attributes(jarAttributeMap)
    }
}

tasks.register('deobfJar', Jar) {
    from sourceSets.main.output
    archiveClassifier.set("deobf")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(jarAttributeMap)
    }
}
